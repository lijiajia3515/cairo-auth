// maven publish config
apply plugin: "maven-publish"
ext {
  versionRelease = !version.endsWith('SNAPSHOT')
}
publishing {
  repositories {
    if (versionRelease) {
      if (project.hasProperty('repository.release.url')) {
        maven {
          url project.property('repository.release.url')
          if (project.hasProperty('repository.release.username')) {
            credentials {
              username project.property('repository.release.username')
              password project.property('repository.release.password')
            }
          }
        }
      }
    } else {
      if (project.hasProperty('repository.snapshot.url')) {
        maven {
          url project.property('repository.snapshot.url')
          if (project.hasProperty('repository.snapshot.username')) {
            credentials {
              username project.property('repository.snapshot.username')
              password project.property('repository.snapshot.password')
            }
          }
        }
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java

      pom {
        name = project.name
        packaging = "jar"
        url = "https://codeup.aliyun.com/harpoon/common/check-java"

        scm {
          url = "https://codeup.aliyun.com/harpoon/common/check-java"
          connection = "scm:git:git://codeup.aliyun.com/harpoon/common/check-java"
          developerConnection = "scm:git:git://codeup.aliyun.com/harpoon/common/check-java"
        }

        issueManagement {
          system = "CodeUp"
          url = "https://codeup.aliyun.com/harpoon/common/check-java/issues"
        }

        licenses {
          license {
            name = "Apache License, Version 2.0"
            url = "https://www.apache.org/licenses/LICENSE-2.0"
            distribution = "repo"
          }
        }

        organization {
          name = "浩方恒科"
          url = "http://www.hfhksoft.com"
        }

        developers {
          developer {
            id = "lijiajia"
            name = "李佳佳"
            email = "lijiajia@haofangsoft.com"
          }
        }
      }
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
    }
  }
}
